function DreamcatcherPlatforms(instance: number):
    broadcast "A"

function DreamcatcherFightAmbience(e: entity):
    DreamcatcherFightMusic({_e})
    DreamcatcherPathfind({_e})
    set {_duration} to 0
    play sound "dungeons.siegeofspawn.dreamcatcher_wind_ambient" in ambient category with volume 5 and pitch 1 at {_e}
    while {_e} is alive:
        if {_duration} >= 54:
            play sound "dungeons.siegeofspawn.dreamcatcher_wind_ambient" in ambient category with volume 5 and pitch 1 at {_e}
            set {_duration} to 0
        wait 1 second
        add 1 to {_duration}
        stop loop if {_e} is not alive
    stop sound "dungeons.siegeofspawn.dreamcatcher_wind_ambient" in ambient category for all players
    
    #elongate sounds if boss fight is longer than 1 min 53 sec
    #stop sound if dreamcatch si dead or players left

function DreamcatcherFightMusic(e: entity):
    set {_duration} to 0
    play sound "dungeons.siegeofspawn.dreamcatcher_fight" in ambient category with volume 5 and pitch 1 at {_e}
    while {_e} is alive:
        if {_duration} >= 181:
            play sound "dungeons.siegeofspawn.dreamcatcher_fight" in ambient category with volume 5 and pitch 1 at {_e}
            set {_duration} to 0
        wait 1 second
        add 1 to {_duration}
        stop loop if {_e} is not alive
    stop sound "dungeons.siegeofspawn.dreamcatcher_fight" in ambient category for all players

function DreamcatcherSheepBeds(e: entity, instance: number):
    loop {-dungeonInstance%{_instance}%::*}:
        push loop-value forward with speed 0.05
    wait 8 seconds
    set {_bedsActive} to 0
    while {_e} is alive:
        set {_randombed} to a random integer between 1 and 4
        if metadata value "Bed1" of {_e} is set:
            add 1 to {_bedsActive}
        if metadata value "Bed2" of {_e} is set:
            add 1 to {_bedsActive}
        if metadata value "Bed3" of {_e} is set:
            add 1 to {_bedsActive}
        if metadata value "Bed4" of {_e} is set:
            add 1 to {_bedsActive}
        if 4 > {_bedsActive}:
            while metadata value "Bed%{_randombed}%" of {_e} is set:
                set {_randombed} to a random integer between 1 and 4
            set metadata value "Bed%{_randombed}%" of {_e} to true
            set {_bedLocation} to yaml value "DreamcatcherBed%{_randombed}%" from "SOSspawnlocations"
            set block at location 1 east of {_bedLocation} to red bed[facing=east,part=head]
            set block at {_bedLocation} to red bed[facing=east,part=foot]
            set yaw of {_bedLocation} to 90
            play sound "entity.sheep.ambient" with volume 4 and pitch 1 at {_e}
            play sound "block.wood.place" with volume 4 and pitch 1 at {_e}
            set {_keystoneId} to metadata value "KeystoneId" of {_e}
            set {_partySize} to tag "custom;ECPartySize" of nbt of {_e}
            set {_instance} to tag "custom;ECDungeonInstance" of nbt of {_e}
            set {_affixes::*} to tag "custom;Affixes" of nbt of {_e}
            set {_sheep} to SOSMobSpawn((location 0.5 above {_bedLocation}), sheep, "Sleeping Sheep", air, "Normal", 0, {_keystoneId}, {_partySize}, 10, {_instance}, "Ranged", true, {_affixes::*})
            set {_sheep} to {_sheep} parsed as entity
            set tag "custom;ECImmuneToCC" of nbt of {_sheep} to true
            set tag "custom;DreamcatcherDreamSheep" of nbt of {_sheep} to true
            set tag "custom;SheepBed" of nbt of {_sheep} to {_randombed}
            set tag "custom;SheepBoss" of nbt of {_sheep} to uuid of {_e}
            #DreamcatcherHealSheepDreams({_e}, {_sheep})
            add nbt from "{NoAI:1b,Silent:1b}" to nbt of {_sheep}
            play sound "dungeons.siegeofspawn.dreamcatcher_humming" with volume 5 and pitch 1 at {_e}
            set {_sheepAmount} to metadata value "SheepAmount" of {_e}
            add 1 to {_sheepAmount}
            set metadata value "SheepAmount" of {_e} to {_sheepAmount}
            set tag "Invulnerable" of nbt of {_e} to true
            set {_rng} to a random integer between 1 and 3
            if {_rng} = 1:
                set {_msg} to "<##d4e4ff>Dreamcatcher &8> &fI'm out of dream energy... Quick! Somebody fall asleep!"
                set {_sound} to "dungeons.siegeofspawn.dreamcatcher_sheep_spawn1"
            else if {_rng} = 2:
                set {_msg} to "<##d4e4ff>Dreamcatcher &8> &fLullaby... ♫ And good night... ♫"
                set {_sound} to "dungeons.siegeofspawn.dreamcatcher_sheep_spawn2"
            else:
                set {_msg} to "<##d4e4ff>Dreamcatcher &8> &fWhy is it that anytime I harness the power of someone's dreams, I also get sheep?"
                set {_sound} to "dungeons.siegeofspawn.dreamcatcher_sheep_spawn_first"
            loop {-dungeonInstance%{_instance}%::*}:
                play sound {_sound} with volume 3 and pitch 1 to loop-value
                send {_msg} to loop-value
        set {_bedsActive} to 0
        wait 32 seconds  
    #Check if a bed is already present
    #When a new bed is placed, assign a metadata value to the number of the bed
    #When a bed is active, heal dreamcatcher for an ammount
    #If 4 beds are active, apply immunity to dreamcatcher
    #Clear metadata value when a sheep dies
    #Disable fall damage for sheep
    #Assign stuff for sheep
    #Restore stone platforms SET A AREARELOADER REGION AND WHEN A DUNGEON IS FINISH ALSO RELOAD THE PLATFORMS THE NBT STAYS
    #On server close loop all stone platforms and clear the dreamcatcherplatformtriggered nbt tag
    #Clear beds on dungeon leave, dungeon finish, dreamcatcher death

#Healing from active sheeps
#function DreamcatcherHealSheepDreams(e: entity, sheep: entity):
#    while {_sheep} is alive:
#        heal {_e} by 0.01*max health of {_e}
#        set {_shape} to a line from {_e} to {_sheep}
#        set particle of {_shape} to dust particle using dustOption(white, 1)
#        draw {_shape}
#        stop loop if {_e} is not alive
#        wait 3 seconds

on death of sheep:
    if tag "custom;ECMob" of nbt of victim is set:
        if tag "custom;DreamcatcherDreamSheep" of nbt of victim is set:
            set {_bossuuid} to tag "custom;SheepBoss" of nbt of victim
            set {_boss} to {_bossuuid} parsed as entity
            set {_bed} to tag "custom;SheepBed" of nbt of victim
            set {_instance} to tag "custom;ECDungeonInstance" of nbt of {_boss}
            clear metadata value "Bed%{_bed}%" of {_boss}
            set {_sheepAmount} to metadata value "SheepAmount" of {_boss}
            remove 1 from {_sheepAmount}
            set metadata value "SheepAmount" of {_boss} to {_sheepAmount}
            if {_sheepAmount} <= 0:
                clear tag "Invulnerable" of nbt of {_boss}
                set {_rng} to a random integer between 1 and 4
                if {_rng} = 1:
                    set {_msg} to "<##d4e4ff>Dreamcatcher &8> &fUgh, what is it with YOU and waking people UP?"
                else if {_rng} = 2:
                    set {_msg} to "<##d4e4ff>Dreamcatcher &8> &fWill you stop. Doing. THAT?!"
                else if {_rng} = 3:
                    set {_msg} to "<##d4e4ff>Dreamcatcher &8> &fAgh! My dream power! Oh- I mean, it's okay you just did that. No biggie."
                else:
                    set {_msg} to "<##d4e4ff>Dreamcatcher &8> &fI said stop it. Stop it, stop it, STOP IT!!!"
                loop {-dungeonInstance%{_instance}%::*}:
                    play sound "dungeons.siegeofspawn.dreamcatcher_sheep_slain%{_rng}%" with volume 3 and pitch 1 to loop-value
                    send {_msg} to loop-value
            set {_bedLocation} to yaml value "DreamcatcherBed%{_bed}%" from "SOSspawnlocations"
            set {_bedLocation2} to block at location 1 east of {_bedLocation}
            set block at {_bedLocation} to air
            set block at {_bedLocation2} to air
            play sound "block.wood.break" with volume 4 and pitch 1 at victim
    
on death of ghast:
    if tag "custom;ECBoss" of nbt of victim is set:
        if name of victim contains "Dreamcatcher":
            set {_instance} to tag "custom;ECDungeonInstance" of nbt of victim
            loop {-dungeonInstance%{_instance}%::*}:
                send "<##d4e4ff>Dreamcatcher &8> &fNOOOOOOOO!" to loop-value
                play sound "dungeons.siegeofspawn.dreamcatcher_defeat" with volume 3 and pitch 1 to loop-value
            loop 4 times:
                set {_bedLocation} to yaml value "DreamcatcherBed%loop-number%" from "SOSspawnlocations"
                set block at {_bedLocation} to air
                set block at (block at location 1 east of {_bedLocation}) to air

function DreamcatcherStraySheep(e: entity):
    wait 13 seconds
    while {_e} is alive:
        loop all blocks in region "dreamcatcher" where [input is not air]:
            if loop-block is not barrier:
                if loop-block is not red bed:
                    add loop-block to {_blocks::*}
        set {_randomBlock} to a random element of {_blocks::*}
        broadcast {_randomBlock}
        set {_loc} to location 4 above {_randomBlock}
        set {_keystoneId} to metadata value "KeystoneId" of {_e}
        set {_partySize} to tag "custom;ECPartySize" of nbt of {_e}
        set {_instance} to tag "custom;ECDungeonInstance" of nbt of {_e}
        set {_affixes::*} to tag "custom;Affixes" of nbt of {_e}
        set {_sheep} to SOSMobSpawn({_loc}, sheep, "Disoriented Sheep", air, "Normal", 0, {_keystoneId}, {_partySize}, 6, {_instance}, "Ranged", true, {_affixes::*})
        set {_sheep} to {_sheep} parsed as entity
        play sound "entity.puffer_fish.blow_up" with volume 4 and pitch 1 at {_randomBlock}
        SheepJump({_sheep})
        loop {-dungeonInstance%{_instance}%::*}:
            send "<##d4e4ff>Dreamcatcher &8> &fHahahahaha. Watch your step!" to loop-value
            play sound "dungeons.siegeofspawn.dreamcatcher_disorientedsheep_spawn" with volume 3 and pitch 1 to loop-value
        #Check sheep's facing 
        wait 18 seconds

function SheepJump(sheep: entity):
    while {_sheep} is alive:
        wait 2.5 seconds
        push {_sheep} upwards with velocity 0.7
        push {_sheep} forwards with speed 0.5

function DreamcatcherTeleport(e: entity):
    set {_DreamcatcherLoc} to yaml value "Dreamcatcher" from "SOSspawnlocations"
    set {_yloc} to y-coordinate of {_DreamcatcherLoc}
    while {_e} is alive:
        if distance between {_e} and {_DreamcatcherLoc} >= 50:
            teleport {_e} to {_DreamcatcherLoc}
        else if y-coordinate of {_e}'s location >= {_yloc} + 20:
            teleport {_e} to {_DreamcatcherLoc}
        wait 5 seconds

function DreamcatcherTornado(e: entity, instance: number):
    wait 6 seconds
    while {_e} is alive:
        set {_randomMsg} to a random integer between 1 and 3
        if {_randomMsg} = 1:
            set {_msg} to "<##d4e4ff>Dreamcatcher &8> &fA Dream Tornado warning is in effect!"
        else if {_randomMsg} = 2:
            set {_msg} to "<##d4e4ff>Dreamcatcher &8> &fPrepare to be blown away!"
        else:
            set {_msg} to "<##d4e4ff>Dreamcatcher &8> &fTime to take you for a little spin!"
        loop {-dungeonInstance%{_instance}%::*}:
            send {_msg} to loop-value
            play sound "dungeons.siegeofspawn.dreamcatcher_dreamtornado%{_randomMsg}%" with volume 3 and pitch 1 to loop-value
        set {_target} to a random element of {-dungeonInstance%{_instance}%::*}
        make {_e} look at {_target}
        set {_loc2} to location(x-coordinate of {_e}, 104, z-coordinate of {_e}, world "%world of {_e}%")
        set yaw of {_loc2} to yaw of {_e}
        loop 3 times:
            set {_loc3} to location (3-loop-number) below {_loc2}
            #set pitch of {_loc3} to 0
            set {_rad} to (loop-number*0.5)
            loop 30 times:
                set {_shape} to a circle with radius {_rad}
                set particle of {_shape} to effect particle
                draw {_shape} at {_loc3}
                set {_loc3} to location 0.1 below {_loc3}
                set {_rad} to {_rad}-0.05
            wait 1 second
            play sound "entity.creeper.hurt" with volume 2.5 and pitch loop-number*0.65 at {_loc2}
        play sound "entity.generic.extinguish_fire" with volume 3 and pitch 2 at {_loc2}
        #set pitch of {_loc2} to 0
        play sound "dungeons.siegeofspawn.dreamcatcher_tornado" with volume 3 and pitch 1 at {_loc2}
        loop 20 times:
            set {_loc} to location loop-number in front of {_loc2}
            #set pitch of {_loc} to 0
            set {_rad} to 1.5
            loop 30 times:
                set {_shape} to a circle with radius {_rad}
                set particle of {_shape} to effect particle
                draw {_shape} at {_loc}
                set {_loc} to location 0.1 below {_loc}
                set {_rad} to {_rad}-0.05
            loop all entities in radius 1.5 around {_loc}:
                if cooldown "DreamcatcherTornadoDamaged%uuid of loop-entity%" is finished:
                    set cooldown "DreamcatcherTornadoDamaged%uuid of loop-entity%" to 0.5 seconds
                    if canBeCCed(loop-entity) = true:
                        push loop-entity upwards with speed 0.12
                        push loop-entity backwards with speed 0.5
                        DamageProperties(loop-entity, {_e}, "<##d4e4ff>Tornado Snów", "<##d4e4ff>Dream Tornado", {_lore}, {_loreEN}, 3.5)
            wait 6 ticks
        set {_randomWait} to a random integer between 2 and 5
        set {_wait} to "%{_randomWait}% seconds" parsed as timespan
        wait {_wait}

on walk on blue glazed terracotta:
    if "%region at event-block%" contains "DreamcatcherPlatforms":
        if metadata value "DreamcatcherEngaged" of player is set:
            loop all blocks in radius 6 around the player where [input = blue glazed terracotta]:
                if ("%regions at loop-block%") = ("%regions at event-block%"):
                    add loop-block to {_blocks::*}
            if {_blocks::*} is set:
                play sound "block.stone_pressure_plate.click_off" with volume 4 and pitch 1 at event-block
                loop {_blocks::*}:
                    set block at loop-value to yellow wool
                wait 1.25 second
                play sound "block.stone_pressure_plate.click_off" with volume 4 and pitch 1.5 at event-block
                loop {_blocks::*}:
                    set block at loop-value to orange wool
                wait 1.25 second
                loop {_blocks::*}:
                    set block at loop-value to red wool
                play sound "block.stone_pressure_plate.click_off" with volume 4 and pitch 2 at event-block
                wait 1.25 second
                play sound "block.stone.break" with volume 3 and pitch 0.8 at event-block
                loop {_blocks::*}:
                    set block at loop-value to air
                    spawn falling block of blue glazed terracotta at loop-value
                    FallingBlockDelete(last spawned falling blue glazed terracotta block)
                play sound "block.lava.extinguish" with volume 3 and pitch 1 at event-block
                wait 5 seconds
                loop {_blocks::*}:
                    set block at loop-value to blue glazed terracotta

function FallingBlockDelete(e: entity):
    wait 5 seconds
    kill {_e}

function DreamcatcherPathfind(e: entity):
    set {_nums::*} to 2, 3, 4, 5
    while {_e} is alive:
        set {_num} to a random element of {_nums::*}
        set {_nums::*} to 1, 2, 3, 4, 5
        remove {_num} from {_nums::*}
        set {_loc} to yaml value "DreamcatcherFly%{_num}%" from "SOSspawnlocations"
        make {_e} pathfind to {_loc}
        wait 8 seconds

function DreamcatcherCouncilDefeat(e: entity):
    wait 3 seconds
    play sound "dungeons.siegeofspawn.dreamcatcher_council_slain" with volume 3 and pitch 1 to {_e}
    send "<##d4e4ff>Dreamcatcher &8> &fHi, RUINER! Me again." to {_e}
    wait 2.6 seconds
    send "<##d4e4ff>Dreamcatcher &8> &fSince getting the information I want out of those crazy scientists' dreams is taking LONGER than expected, I thought I might slow you down again." to {_e}

function DreamcatcherGoshaDefeat(e: entity):
    wait 3 seconds
    play sound "dungeons.siegeofspawn.dreamcatcher_gosha_slain" with volume 3 and pitch 1 to {_e}
    send "<##d4e4ff>Dreamcatcher &8> &fThis whole hero thing you're doing is getting REALLY tiresome, so I think it's time I finally put an end to it." to {_e}
#sea creature particle + soound (nightmares)

#platform

#set metadata value "DreamcatcherEngaged" of player when dreamcatcher is pulled
#clear metadata value "DreamcatcherEngaged" of player when boss is reset/dies and on dungeon leave

#Add each 4x4 section in each instance to dungeon config
#walka sie zaczyna
#bloki cobblestone w powietrzu (paltforma 4x4 3x3) na start -> dreamcatcher dopelnia zanim walka sie zacznie i mozna jej zadac dmg
#tornado breeze
#owce skaczace o 1
#lozka z owcami -> dreamcatcher jest immune i ma tarcze kolo wokol siebie
#4 sec respawn platformy
#na 90% hp wlacza spadanie blokow (animacja spadania falling block?)