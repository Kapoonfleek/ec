options:
	maxEcs: 6
on join:
	load yaml "plugins/Skript/playerdata/%uuid of player%.yml" as "data-%player%"
	load yaml "plugins/Skript/playerdata/admin/%uuid of player%-Admin.yml" as "dataAdmin-%player%"
	load yaml "plugins/Skript/playerdata/enderchesty/Enderchest-%uuid of player%.yml" as "Enderchest%player%"
	loop {@maxEcs} times:
		load yaml "plugins/Skript/playerdata/enderchesty/contents/%uuid of player%/%loop-number%.yml" as "Enderchest%loop-number%%player%"
	
	if metadata value "ECEngaged" of player is not set:
		SetPlayerStats(player) #Sets player stats to current selected card's stats (funkcje.sk)
		
	if {karta::%player%} is set:
		set {_primaryStatType} to string tag "PrimaryStat" of {karta::%player%}
		set metadata value "PrimaryStatType" of player to {_primaryStatType}
		set {_id} to string tag "Id" of custom nbt of {karta::%player%}
		load yaml "plugins/Skript/playerdata/karty/%{kartaname::%player%}%/%{_id}%.yml" as "kartyPh-%player%"

	set {_color} to raw {kolorek::%player%}
	replace all "<" and ">" in {_color} with ""
	set yaml value "Player.Name" in "data-%player%" to name of player
	set yaml value "Player.Last Login" in "data-%player%" to data()
	set yaml value "Player.Colour" in "data-%player%" to {_color}
	set yaml value "Player.Level" in "data-%player%" to {poziom::%player%}
	set yaml value "Player.Exp" in "data-%player%" to round({exp::%player%}, 2)
	
	set yaml value "Player.Name" in "dataAdmin-%player%" to name of player
	set yaml value "Player.UUID" in "dataAdmin-%player%" to uuid of player
	set yaml value "Player.Location" in "dataAdmin-%player%" to location of player
	if yaml list "Player.Name History" in "dataAdmin-%player%" does not contain player's name:
		add player's name to yaml list "Player.Name History" in "dataAdmin-%player%" 
	
	save yaml "data-%player%"
	save yaml "dataAdmin-%player%"
	set {_size} to size of (yaml list "Player.Item Claim" in "dataAdmin-%player%")
	if {_size} > 0:
		wait 6 seconds
		play sound "block.note_block.harp" with volume 3 and pitch 1.2 to player
		if {language::%player%} = "English":
			send "&eYou have &f%{_size}% &eunclaimed items. Type /itemclaim to claim the items." to player
		else:
			send "&eMasz &f%{_size}% &enieodebranych przedmiotów. Wpisz /itemclaim, by odebrać przedmioty." to player
	if party of player is set:
		set {_partymembers::*} to party members of party of player
		loop {_partymembers::*}:
			add (name of loop-value parsed as player) to {_party::*}
		set {_name} to name of party of player

	
on quit:
	set yaml value "Player.Last Login" in "data-%player%" to now
	save yaml "data-%player%"
	save yaml "dataAdmin-%player%"
	unload yaml "data-%player%"
	unload yaml "dataAdmin-%player%"
	save yaml "Enderchest%player%"
	unload yaml "Enderchest%player%"
	loop {@maxEcs} times:
		save yaml "Enderchest%loop-number%%player%"
		unload yaml "Enderchest%loop-number%%player%"
	
every 1 minute:
	loop all loaded yaml:
		save loop-value
		wait 2 ticks

on skript stop:
	loop all loaded yaml:
		save loop-value
		wait 1 tick