import:
	eu.asangarin.arikeys.api.AriKeyPressEvent
	eu.asangarin.arikeys.api.AriKeyReleaseEvent
	
custom event "key press":
	pattern: key (:press|:release) [of %-string%]
	event-values: string, player, world
	check:
		if parse tags is not extra data "action":
			stop
		if expr-1 is not set:
			continue
		else if event-string is expr-1:
			continue
	
on AriKeyPressEvent:
	set {_event-values::string} to event.getId().getKey()
	set {_event-values::player} to event.getPlayer()
	set {_event-values::world} to event.getPlayer().getWorld()
	set {_extra-data::action} to "press"
	call a new custom event "key press" with event-values {_event-values::*} and data {_extra-data::*}
	
on AriKeyReleaseEvent:
	set {_event-values::string} to event.getId().getKey()
	set {_event-values::player} to event.getPlayer()
	set {_event-values::world} to event.getPlayer().getWorld()
	set {_extra-data::action} to "release"
	call a new custom event "key press" with event-values {_event-values::*} and data {_extra-data::*}
	
on key press:
	broadcast "%event-string% was pressed by %event-player%"
	
on key release:
    broadcast "%event-string% was released by %event-player%"